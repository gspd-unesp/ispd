
options {
  STATIC = false;
}

PARSER_BEGIN(Interpretador)

package InterpretadorInterno.ModeloIconico;

import javax.swing.*;
import javax.swing.filechooser.*;
import java.awt.*;
import java.util.*;
import java.io.*;
import java.awt.event.*;
import java.awt.image.*;  
import java.net.*;  
import javax.imageio.*;
import java.awt.*;
import java.util.HashSet;
import java.util.Vector;
import java.util.List;
import java.util.ArrayList;
import javax.swing.*;
import java.io.*;

class Interpretador{

	public boolean verbose;
	private String textoVerbose = "Saida do Verbose:";
	public boolean erroEncontrado = false;
	private HashSet<DescreveIconePrivado> listaIcones = new HashSet<DescreveIconePrivado>();
	private HashSet<String> listaNomes = new HashSet<String>();
	private HashSet<String> listaEscravos = new HashSet<String>();
	private HashSet<String> listaConexoes = new HashSet<String>();
	private List<String> escravos2 = new ArrayList<String>();
	private String algoritmo = null;
	private String erros = "Erros encontrados durante o parser do Modelo Iconico:";
	private int numIcones = 0;
	private int numArestas = 0;
	private int numVertices = 0;
	private Boolean cargasConfiguradas = new Boolean(false);;
	private Integer cargasTipoConfiguracao = new Integer(-1);
	private String cargasConfiguracao = new String();
	private int numeroConexoes = 0;
	
	public Interpretador(){
	}

	public void printv(String msg){
		textoVerbose = textoVerbose+"\n>"+msg;
	}

	public void addErro(String msg){
		erros = erros+"\n"+msg;
	}

	public void resuladoParser(){
		if(erroEncontrado){
			MostraSaida saida = new MostraSaida("Found Errors",erros);
			saida.setVisible(true);
		}
		else{
			//JOptionPane.showOptionDialog(null,"Modelo iconico reconhecido com sucesso","Arquivo Reconhecido", JOptionPane.DEFAULT_OPTION, JOptionPane.PLAIN_MESSAGE, null, null, null);
			if(verbose){ MostraSaida saida = new MostraSaida("Saida do Reconhecimento",textoVerbose); saida.setVisible(true); }
		}
	}

	public void verificaLabels(){
		for(String temp:listaEscravos){
			if(!listaNomes.contains(temp)){
				erroEncontrado = true;
				addErro("\'"+temp+"\' does not exist!");
			}
		}
		for(String temp:listaConexoes){
			if(!listaNomes.contains(temp)){
				erroEncontrado = true;
				addErro("\'"+temp+"\' does not exist!");
			}
		}
	}

	public void escreveArquivo(){
		try{
			File file = new File("modelosimulavel");
			FileWriter writer = new FileWriter(file);
			PrintWriter saida = new PrintWriter(writer,true);
			saida.println("MODELO");
			saida.println("TAREFA");
			switch(cargasTipoConfiguracao){
				case 0:
					saida.println("RANDOM "+cargasConfiguracao);
					break;
				case 1:
					saida.println("MAQUINA "+cargasConfiguracao);
					break;
				case 2:
					saida.println("TRACE "+cargasConfiguracao);
					break;
			}
			saida.println("FIM_TAREFA");
			saida.println("CENTROS_DE_SERVICOS");
			for(DescreveIconePrivado icone:listaIcones){
				switch(icone.getTipoIcone()){
					case 1: 
						if(icone.getMestre()){
							saida.printf("CS_0 %s 1 1 FILAS fila_%s SERVIDORES serv_%s 0 %f %f ",icone.getNome(),icone.getNome(),icone.getNome(),icone.getPoderComputacional(),icone.getTaxaOcupacao());
							saida.print("MESTRE "+icone.getAlgoritmo()+" LMAQ ");
							List<String> nos = icone.getEscravos();
							for(String no:nos){
								saida.print(""+no+" ");
							}
							saida.println("");
						}else{
							saida.printf("CS_0 %s 1 1 FILAS fila_%s SERVIDORES serv_%s 0 %f %f ",icone.getNome(),icone.getNome(),icone.getNome(),icone.getPoderComputacional(),icone.getTaxaOcupacao());
							saida.print("ESCRAVO");
							saida.println("");
						}
						break;
					case 2: saida.printf("CS_2 %s 1 1 FILAS fila_%s SERVIDORES serv_%s 1 %f %f %f\n",icone.getNome(),icone.getNome(),icone.getNome(),icone.getBanda(),icone.getTaxaOcupacao(),icone.getLatencia());
						break;
					case 4: saida.printf("CS_3 %s 1 1 FILAS fila_%s SERVIDORES serv_%s 1 %f %f %f\n",icone.getNome(),icone.getNome(),icone.getNome(),icone.getBanda(),icone.getTaxaOcupacao(),icone.getLatencia());
						break;
					case 3: saida.print("CS_1 "+icone.getNome()+" 2 "+icone.getNumeroEscravos()+" "+icone.getAlgoritmo()+" FILAS fila_0_"+icone.getNome()+" fila_1_"+icone.getNome()+" SERVIDORES ");
							saida.printf("serv_%s 0 %f %f %f ",icone.getNome(),icone.getPoderComputacional(),icone.getBanda(),icone.getLatencia());
							saida.println("");
						break;
				}
			}
			saida.println("FIM_CENTROS_DE_SERVICOS");
			if(numeroConexoes > 0){
				saida.println("CONEXOES");
				for(DescreveIconePrivado icone:listaIcones){
					if(icone.getTipoIcone()==2){
						saida.printf("%s\t%s\n",icone.getSNoOrigem(),icone.getNome());
						saida.printf("%s\t%s\n",icone.getNome(),icone.getSNoDestino());
					}					
				}
				saida.println("FIM_CONEXOES");
			}
			saida.println("FIM_MODELO");
			saida.close();   
			writer.close();
			//JOptionPane.showMessageDialog(null,"File successfully saved\n","Completed",JOptionPane.INFORMATION_MESSAGE);
		}
		// em caso de erro apresenta mensagem abaixo  
		catch(Exception e){  
			JOptionPane.showMessageDialog(null,e.getMessage(),"Warning",JOptionPane.WARNING_MESSAGE);  
		}
		
		
	}

	public void consomeTokens(){
		Token t = getToken(1);
		while( t.kind != MAQ && t.kind != REDE && t.kind != INET && t.kind != CLUSTER && t.kind != CARGA && t.kind != EOF){
			getNextToken();
			t = getToken(1);
		}
	}

	public void resetaObjetosParser(){
		textoVerbose = "";
		erroEncontrado = false;
		listaIcones.clear();
		numIcones = 0;
		numVertices = 0;
		numArestas = 0;
		listaNomes.clear();
		listaEscravos.clear();
		listaConexoes.clear();
		escravos2.clear();
		String algoritmo = null;
		String erros = "Foram encontrados os seguintes erros durante o parser:";
		cargasConfiguradas = new Boolean(false);;
		cargasTipoConfiguracao = new Integer(-1);
		cargasConfiguracao = new String();	
		numeroConexoes = 0;		
	}
	
	private class DescreveIconePrivado {

			private int tipoIcone;
			private int IdGlobal;
			private int IdLocal;
			private String nome;
			private int x,y,prex,prey;
			private double poderComputacional;
			private double taxaOcupacao;
			private double latencia;
			private double banda;
			private int numeroEscravos;
			private boolean mestre;
			private String algoritmoEscalonamento;
			private List<String> escravos;
			private List<Integer> escravos2;
			private int noOrigem;
			private int noDestino;
			private String snoOrigem;
			private String snoDestino;
			private HashSet<Integer> conexaoEntrada;
			private HashSet<Integer> conexaoSaida;

			public DescreveIconePrivado(){
				conexaoEntrada = new HashSet<Integer>();
				conexaoSaida = new HashSet<Integer>();
			}
			public void setAtributosNo(int tipoIcone,int IdLocal,int IdGlobal,String nome,double poderComputacional,double taxaOcupacao,boolean mestre,String algoritmoEscalonamento,List<String> escravos){
				escravos2 = new ArrayList<Integer>();
				this.tipoIcone = tipoIcone;
				this.IdLocal = IdLocal;
				this.IdGlobal = IdGlobal;
				this.nome = nome;
				this.x = 0;
				this.y = 0;
				this.prex = 0;
				this.prey = 0;
				this.poderComputacional = poderComputacional;
				this.taxaOcupacao = taxaOcupacao;
				this.latencia = 0.0;
				this.banda = 0.0;
				this.mestre = mestre;
				this.escravos = escravos;
				this.algoritmoEscalonamento = algoritmoEscalonamento;
				this.numeroEscravos = 0;
				this.noOrigem = 0;
				this.noDestino = 0;
				this.snoOrigem = null;
				this.snoDestino = null;
			}
			public void setAtributosLink(int tipoIcone,int IdLocal,int IdGlobal,String nome,double taxaOcupacao,double latencia,double banda,String snoOrigem, String snoDestino){
				escravos2 = new ArrayList<Integer>();
				this.tipoIcone = tipoIcone;
				this.IdLocal = IdLocal;
				this.IdGlobal = IdGlobal;
				this.nome = nome;
				this.x = 0;
				this.y = 0;
				this.prex = 0;
				this.prey = 0;
				this.poderComputacional = 0.0;
				this.taxaOcupacao = taxaOcupacao;
				this.latencia = latencia;
				this.banda = banda;
				this.mestre = false;
				this.escravos = new ArrayList<String>();
				this.algoritmoEscalonamento = null;
				this.numeroEscravos = 0;
				this.noOrigem = 0;
				this.noDestino = 0;
				this.snoOrigem = snoOrigem;
				this.snoDestino = snoDestino;
			}
			public void setAtributosCluster(int tipoIcone,int IdLocal,int IdGlobal,String nome,double poderComputacional,double latencia,double banda, String algoritmoEscalonamento, int numeroEscravos){
				escravos2 = new ArrayList<Integer>();
				this.tipoIcone = tipoIcone;
				this.IdLocal = IdLocal;
				this.IdGlobal = IdGlobal;
				this.nome = nome;
				this.x = 0;
				this.y = 0;
				this.prex = 0;
				this.prey = 0;
				this.poderComputacional = poderComputacional;
				this.taxaOcupacao = 0.0;
				this.latencia = latencia;
				this.banda = banda;
				this.mestre = false;
				this.escravos = new ArrayList<String>();
				this.algoritmoEscalonamento = algoritmoEscalonamento;
				this.numeroEscravos = numeroEscravos;
				this.noOrigem = 0;
				this.noDestino = 0;
				this.snoOrigem = null;
				this.snoDestino = null;
			}
			public void setAtributosInet(int tipoIcone,int IdLocal,int IdGlobal,String nome,double taxaOcupacao,double latencia,double banda){
				escravos2 = new ArrayList<Integer>();
				this.tipoIcone = tipoIcone;
				this.IdLocal = IdLocal;
				this.IdGlobal = IdGlobal;
				this.nome = nome;
				this.x = 0;
				this.y = 0;
				this.prex = 0;
				this.prey = 0;
				this.poderComputacional = 0.0;
				this.taxaOcupacao = taxaOcupacao;
				this.latencia = latencia;
				this.banda = banda;
				this.mestre = false;
				this.escravos = new ArrayList<String>();
				this.algoritmoEscalonamento = null;
				this.numeroEscravos = 0;
				this.noOrigem = 0;
				this.noDestino = 0;
				this.snoOrigem = null;
				this.snoDestino = null;
			}
			public void addConexaoEntrada(int num){conexaoEntrada.add(num);}
			public void addConexaoSaida(int num){conexaoSaida.add(num);}
			public void addEscravos2(int id){escravos2.add(id);}
			public int getTipoIcone(){return  tipoIcone;}
			public int getIdGlobal(){return  IdGlobal;}
			public int getIdLocal(){return  IdLocal;}
			public String getNome(){return  nome;}
			public int getNumX(){return  x;}
			public int getNumY(){return  y;}
			public void setX(int x){this.x = x;}
			public void setY(int y){this.y = y;}
			public int getNumPreX(){return  prex;}
			public int getNumPreY(){return  prey;}
			public void setPreX(int x){this.prex = x;}
			public void setPreY(int y){this.prey = y;}
			public double getPoderComputacional(){return  poderComputacional;}
			public double getTaxaOcupacao(){return  taxaOcupacao;}
			public double getLatencia(){return  latencia;}
			public double getBanda(){return  banda;}
			public boolean getMestre(){return  mestre;}
			public List<String> getEscravos(){return  escravos;}
			public List<Integer> getEscravos2(){return  escravos2;}
			public String getAlgoritmo(){return  algoritmoEscalonamento;}
			public int getNumeroEscravos(){return numeroEscravos;}
			public void setNoOrigem(int no){this.noOrigem = no;}
			public void setNoDestino(int no){this.noDestino = no;}
			public int getNoOrigem(){return noOrigem;}
			public int getNoDestino(){return noDestino;}
			public String getSNoOrigem(){return snoOrigem;}
			public String getSNoDestino(){return snoDestino;}
			public HashSet<Integer> getConexaoEntrada(){return conexaoEntrada;}
			public HashSet<Integer> getConexaoSaida(){return conexaoSaida;}
	}
}
PARSER_END(Interpretador)



TOKEN_MGR_DECLS:
{
private int contaErrosLex = 0;
private String erros = new String("");


public int encontrouErroLex()
{
	return contaErrosLex;
}

public void addErro(String msg)
{
	erros = erros+"\n"+msg;
}

public String getErros()
{
	return erros;
}

}


TOKEN:	{ 
	<RR: "RoundRobin"> |
	<MAQ: "MAQ"> |
	<VAZIO: "---"> |
	<LMAQ: "LMAQ"> |
	<REDE: "REDE"> |
	<INET: "INET"> |
	<FPLTF:	"FPLTF"> |
	<CARGA: "CARGA"> |
	<TRACE: "TRACE"> |
	<MESTRE: "MESTRE"> |
	<RANDOM: "RANDOM"> |
	<POISSON: "POISSON"> |
	<ESCRAVO: "ESCRAVO"> |
	<CLUSTER: "CLUSTER"> |
	<CONECTA: "CONECTA"> |
	<MAQUINA: "MAQUINA"> |
	<TWOSTAGE: "TWOSTAGE"> |
	<WORKQUEUE: "WORKQUEUE"> |
	<EXPONENCIAL: "EXPONENCIAL">
	} 
//strings mais curtas antes para melhorar o desempenho do lexer

TOKEN:	{< especial: ( "!" | "@" | "#" | "$" | "%" | "&" | "(" | ")" | "-" | "_" | "+" | "=" | "{" | "}" | "[" | "]" | "~" | "^" | ";" | "." | "," | "|" | ":" | "\\" | "/" ) >}

TOKEN:	{< nome: (["A"-"Z","a"-"z"])+((<especial>) | (["0"-"9"]) | (["A"-"Z","a"-"z"]))* >}

TOKEN:	{< inteiro: (["0"-"9"])+ >}

TOKEN:	{< flutuante: (["0"-"9"])+"."(["0"-"9"])+ >}

SKIP: { "\n" | " " | "\t" | "\b" | < "#" (~["\r","\n"])* ("\n" | "\r" | "\r\n") > }

SPECIAL_TOKEN:
{
<invalido: (~["A"-"Z","a"-"z","0"-"9","!" , "@" , "#" , "$" , "%" , "&" , "(" , ")" , "-" , "_" , "+" , "=" , "{" , "}" , "[" , "]" , "~" , "^" , ";" , "." , "," , "|" , ":" , "\\" , "/", "\n" , " " , "\t" , "\b" , "#" ])+>
	{
		addErro("Erro na linha "+input_stream.getEndLine()+". Caracter "+image+" não é aceito.");
		contaErrosLex++;
	}
}


void Modelo():
{
	resetaObjetosParser();
 }
{
	try{
		Icones() <EOF> 
		{
			printv("Reconheceu Modelo()");
	
			verificaLabels();	
	
			resuladoParser();
		}
	}
	catch(ParseException e){
		Token t = getToken(1);
		addErro("Erro semantico encontrado na linha "+t.endLine+", coluna "+t.endColumn);
		erroEncontrado = true;
		consomeTokens();
		resuladoParser();
	}
}

void Icones():
{

}
{
	 (Icone())+ 
		{ printv("Reconheceu Icones"); }
}

void Icone():
{

}
{
	No()	
		{ printv("Reconheceu No()"); }
	| Cluster() 
		{ printv("Reconheceu Cluster()"); }
	| Link()
		{ printv("Reconheceu Link()"); }
	| Inet()
		{ printv("Reconheceu Inet()"); }
	| Carga()
		{ printv("Reconheceu Carga()"); }
}

boolean NoTipo():
{
	Token t;
}
{
	<MESTRE> algoritmo=ClusterAlg() <LMAQ> NoLista() { return true; }
	| <ESCRAVO> { return false; }
}

void NoLista():
{
	Token t;
	String s;
	escravos2.clear();
}
{
	(t=<nome>{s = t.image; escravos2.add(s); listaEscravos.add(s); printv("Reconheceu nome no escravo");} )+
}

void No():
{
	Boolean mestre;
	Token t2, t3, t4;
	DescreveIconePrivado icone = new DescreveIconePrivado();
	List<String> escravos = new ArrayList<String>();
}
{
	try{
		<MAQ> t2=<nome> t3=<flutuante> t4=<flutuante> mestre=NoTipo() 
		{
			if(listaNomes.contains(t2.image)){
				addErro("There's already an icon named \'"+t2.image+"\'.");
				erroEncontrado = true;
			}else{
				listaNomes.add(t2.image);
			}
		
			if(mestre){icone.setAtributosNo(1, numVertices, numIcones, t2.image, Double.parseDouble(t3.image), Double.parseDouble(t4.image), true, algoritmo, escravos2);
					listaIcones.add(icone); 
					numIcones++;
					numVertices++;
					algoritmo = null;
				}
			else{icone.setAtributosNo(1, numVertices, numIcones, t2.image, Double.parseDouble(t3.image), Double.parseDouble(t4.image), false, algoritmo, escravos);
					listaIcones.add(icone); 
					numIcones++;
					numVertices++;
				}
		}
	}
	catch(ParseException e){
		Token t = getToken(1);
		addErro("Erro semantico encontrado na linha "+t.endLine+", coluna "+t.endColumn);
		erroEncontrado = true;
		consomeTokens();
	}

}

void Link():
{
	Token t2, t3, t4, t5, t6, t7;
	DescreveIconePrivado icone = new DescreveIconePrivado();
}
{
	try{
		<REDE> t2=<nome> t3=<flutuante> t4=<flutuante> t5=<flutuante> <CONECTA> t6=<nome> t7=<nome>
		{
			if(listaNomes.contains(t2.image)){
				addErro("There's already an icon named \'"+t2.image+"\'.");
				erroEncontrado = true;
			}else{
				listaNomes.add(t2.image);
			}
			
			if(!listaConexoes.contains(t6.image)){ listaConexoes.add(t6.image); }
			if(!listaConexoes.contains(t7.image)){ listaConexoes.add(t7.image); }
			icone.setAtributosLink(2, numArestas, numIcones , t2.image, Double.parseDouble(t5.image), Double.parseDouble(t4.image), Double.parseDouble(t3.image), t6.image, t7.image);
			listaIcones.add(icone);
			numArestas++;
			numIcones++;
			numeroConexoes++;
		}
	}
	catch(ParseException e){
		Token t = getToken(1);
		addErro("Erro semantico encontrado na linha "+t.endLine+", coluna "+t.endColumn);
		erroEncontrado = true;
		consomeTokens();
	}
}

String ClusterAlg():
{
	Token t;
	String s;
}
{
	t=<RR> { s = t.image; return s;}
	| t=<WORKQUEUE> { s = t.image; return s;}
	| t=<FPLTF> { s = t.image; return s;}
	| t=<VAZIO> { s = t.image; return s;}
}

void Cluster():
{
	String s;
	Token t2, t3, t4, t5, t6, t7;
	DescreveIconePrivado icone = new DescreveIconePrivado();
}
{
	try{
		<CLUSTER> t2=<nome> t3=<inteiro> t4=<flutuante> t5=<flutuante> t6=<flutuante> s=ClusterAlg()
		{
			if(listaNomes.contains(t2.image)){
				addErro("There's already an icon named \'"+t2.image+"\'.");
				erroEncontrado = true;
			}else{
				listaNomes.add(t2.image);
			}
			icone.setAtributosCluster(3, numVertices, numIcones, t2.image, Double.parseDouble(t4.image), Double.parseDouble(t6.image), Double.parseDouble(t5.image), s,  Integer.parseInt(t3.image));
			listaIcones.add(icone);
			numIcones++;
			numVertices++;
		}
	}
	catch(ParseException e){
		Token t = getToken(1);
		addErro("Erro semantico encontrado na linha "+t.endLine+", coluna "+t.endColumn);
		erroEncontrado = true;
		consomeTokens();
	}
}

void Inet():
{
	Token t2, t3, t4, t5, t6, t7;
	DescreveIconePrivado icone = new DescreveIconePrivado();
	//escravos.clear();
	HashSet<String> nosConectados = new HashSet<String>();
	List<String> escravos = new ArrayList<String>();
}
{
	try{
		<INET> t2=<nome> t3=<flutuante> t4=<flutuante> t5=<flutuante>
		{	
			if(listaNomes.contains(t2.image)){
				addErro("There's already an icon named \'"+t2.image+"\'.");
				erroEncontrado = true;
			}else{
				listaNomes.add(t2.image);
			}
			icone.setAtributosInet(4, numVertices,numIcones, t2.image, Double.parseDouble(t5.image), Double.parseDouble(t4.image), Double.parseDouble(t3.image));
			listaIcones.add(icone);
			numIcones++;
			numVertices++;
		}
	}
	catch(ParseException e){
		Token t = getToken(1);
		addErro("Erro semantico encontrado na linha "+t.endLine+", coluna "+t.endColumn);
		erroEncontrado = true;
		consomeTokens();
	}
}

void Carga():
{
	cargasConfiguradas = true;
}
{
	try{
		<CARGA> TipoCarga()
	}
	catch(ParseException e){
		Token t = getToken(1);
		addErro("Erro semantico encontrado na linha "+t.endLine+", coluna "+t.endColumn);
		erroEncontrado = true;
		consomeTokens();
	}
}

void TipoCarga():
{
}
{
		<RANDOM> TipoRandom()
	|	<MAQUINA> (TipoMaquina())+
	|	<TRACE> TipoTrace()
}

void TipoRandom():
{
	String dist;
	Token t1,t2,t3,t4,t5,t6,t7,t8,t9,t10,t11;
	cargasTipoConfiguracao = 0;
}
{
	t1=<inteiro> t2=<inteiro> t3=<inteiro> t4=<flutuante> t5=<inteiro> t6=<inteiro> t7=<inteiro> t8=<flutuante> t9=<inteiro> t10=<inteiro> t11=<inteiro> 
	{
		cargasConfiguracao = t1.image+" "+t2.image+" "+t3.image+" "+t4.image+"\n"+t5.image+" "+t6.image+" "+t7.image+" "+t8.image+"\n"+t9.image+" "+t10.image+" "+t11.image;
	}
}

String Distribuicao():
{
	Token t;
}
{
		t=<POISSON> {return t.image;} 
	|	t=<EXPONENCIAL> {return t.image;}
	|	t=<TWOSTAGE> {return t.image;}
}

void TipoMaquina():
{
	String dist;
	Token t1,t2,t3,t4,t5,t6;
	cargasTipoConfiguracao = 1;
}
{
	t1=<nome> t2=<inteiro> t3=<flutuante> t4=<flutuante> t5=<flutuante> t6=<flutuante>
	{
		cargasConfiguracao += ""+t1.image+" "+t2.image+" "+t3.image+" "+t4.image+" "+t5.image+" "+t6.image+"\n";
	}
}

void TipoTrace():
{
	Token t;
	cargasTipoConfiguracao = 2;
}
{
	t=<nome>
	{
		cargasConfiguracao = t.image+"\n";
	}
}
